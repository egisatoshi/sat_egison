(define $x 5)

(define $my-multiset
  (lambda [$a]
    (matcher
      {[<nil> [] {[{} {[]}] [_ {}]}]
       [<cons $ $> [a (my-multiset a)]
        {[$tgt (match-all tgt (list a)
                 [<join $hs <cons $x $ts>> [x (append hs ts)]])]}]
       [,$val []
        {[$tgt (match [val tgt] [(list a) (my-multiset a)]
                {[[<nil> <nil>] {[]}]
                 [[<cons $x $xs> <cons ,x ,xs>] {[]}]
                 [[_ _] {}]})]}]
       [$ [something] {[$tgt {tgt}]}]})))

(define $mem?
  (lambda [$xs $x]
    (match xs (set something) {[<cons ,x _> #t] [_ #f]})))

(define $all-ref
  (lambda [$all $var]
    (if (mem? all var) <T> <F>)))

(define $neg
  (lambda [$val]
    (match val something {[,<T> <F>] [,<F> <T>]})))

(define $literal
  (matcher {[<p $> [nat] {[<Pos $x> {x}] [_ {}]}]
            [<n $> [nat] {[<Neg $x> {x}] [_ {}]}]
            [,$l [] {[$tgt (if (eq? tgt l) {[]} {})]}]
            [$ [something] {[$tgt {tgt}]}]}))

(define $eval-literal
  (lambda [$all $lit]
    (match lit literal {[<p $var> (all-ref all var)]
                        [<n $var> (neg (all-ref all var))]})))

(define $eval-cls
  (lambda [$all $cls]
    (match cls (list something)
      {[<nil> <F>]
       [<cons $lit $rst> (match (eval-literal all lit) something
                           {[,<T> <T>]
                            [,<F> (eval-cls all rst)]})]})))

(define $eval-cnf
  (lambda [$all $cnf]
    (match cnf (list something)
      {[<nil> <T>]
       [<cons $cls $rst> (match (eval-cls all cls) something
                           {[,<T> (eval-cnf all rst)]
                            [,<F> <F>]})]})))

;; (define $sat
;;   (lambda [$n $cnf] ; n is the number of variables in cnf
;;     (let {[$vars (between 1 n)]}
;;       (match-all vars (multiset integer)
;;         [<join (& $all ?(lambda [$all] (eq? (eval-cnf all cnf) <T>))) _> all]))))

(define $allocate-true
  (lambda [$cnf $var]
    (match-all cnf (set (set literal))
      [<cons (& $cls !<cons <p ,var> _>) _> (delete <Neg var> cls)])))

(define $allocate-false
  (lambda [$cnf $var]
    (match-all cnf (set (set literal))
      [<cons (& $cls !<cons <n ,var> _>) _> (delete <Pos var> cls)])))

(define $delete-literal
  (lambda [$lit $cnf $vars]
    (match lit literal
      {[<p $x> [(allocate-true cnf x) (delete x vars)]]
       [<n $x> [(allocate-false cnf x) (delete x vars)]]})))

(define $exist?
  (lambda [$lit $cnf]
    (match cnf (multiset (multiset literal))
      {[<cons <cons ,lit _> _> #t]
       [_ #f]})))

(define $sat
  (lambda [$cnf $vars]
    (match cnf (multiset (multiset literal))
      {[<nil> #t]
       [<cons <nil> _> #f]
       [<cons <cons $lit <nil>> _> (sat (delete-literal lit cnf vars))]
       [_ (match vars (multiset integer)
            {[<nil> "Error"]
             [<cons (& $x !?(lambda [$x] (exist? <Pos x> cnf))) $xs>
              (sat (delete-literal <Neg x> cnf vars))]
             [<cons (& $x !?(lambda [$x] (exist? <Neg x> cnf))) $xs>
              (sat (delete-literal <Pos x> cnf vars))]
             [<cons $x $xs>
              (or (sat (allocate-true cnf x) xs)
                  (sat (allocate-false cnf x) xs))]})]})))

(define $sample-cnf1
  {{<Pos 1> <Pos 2>} {<Pos 1> <Neg 2>} {<Neg 1> <Neg 2>}})
(define $sample-all1-1
  {1 2})
(define $sample-all1-2
  {1})
(define $sample-cnf2
  {{<Pos 1> <Pos 2>} {<Neg 1> <Pos 2>} {<Pos 1> <Neg 2>} {<Neg 1> <Neg 2>}})
